#Область ОписаниеПеременных
&НаКлиенте
Перем КодыА, КодыВ;
#КонецОбласти

#Область ОбработчикиСобытийФормы
// При открытии получает через запросы массивы кодов первого порядка и кодов валют для их дальнейшего использования.
// 
// Параметры:
//  Отказ - Булево - Отказ
&НаКлиенте
Процедура ПриОткрытии(Отказ)
	КодыРС =  УзнатьАиВ();
	КодыА = КодыРС.КодыА;
	КодыВ = КодыРС.КодыВ;
КонецПроцедуры
#КонецОбласти
#Область ОбработчикиКомандФормы
&НаКлиенте
Процедура ЗаполнитьПолеРасчетногоСчета(Команда)
	РасчетныйСчет = СгенерироватьСлучайныйРасчетныйСчет(КодыА, КодыВ);
КонецПроцедуры
// Генерирует указанное в диалоге количество случайных расчетных счетов в файле scheta_random.txt в выбранном каталоге.
// 
// Параметры:
//  Команда - КомандаФормы - Команда
&НаКлиенте
Асинх Процедура ЗаписатьРасчетныеСчетаВФайл(Команда)
	РезДиалога = Ждать ПолучитьРезультатДиалога();
	Если РезДиалога <> Неопределено Тогда
		Начало = ТекДата();
		Файл = Новый ТекстовыйДокумент;
		Для Н = 1 По РезДиалога.Колво Цикл
			СлучайныйСчет =  СгенерироватьСлучайныйРасчетныйСчет(КодыА, КодыВ);
			Файл.ДобавитьСтроку(Формат(СлучайныйСчет, "ЧГ=0"));
		КонецЦикла;
		Файл.Записать(РезДиалога.Файл);
		Конец = ТекДата();
		ДиалогКлиент.Сообщ(СтрШаблон("Файл %1 создан", РезДиалога.Файл));
		ДиалогКлиент.Сообщ(СтрШаблон("Время выполнения %1 сек.", Конец - Начало));
	КонецЕсли;
КонецПроцедуры
#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаКлиенте
Асинх Функция ПолучитьРезультатДиалога()
	Ответ = Ждать ВопросАсинх("Создать файл со случайными РС?", РежимДиалогаВопрос.ДаНет, , , "Создание файла" );
	Если Ответ = КодВозвратаДиалога.Да Тогда
		ДиалогДанные = Новый Структура;
		Колво = Ждать ВвестиЗначениеАсинх(0, "Введите кол-во генерируемых счетов:","Число");
		Если Колво <> Неопределено И Колво > 0 Тогда
			ДиалогДанные.Вставить("Колво", Колво);
			ДиалогВыбораПапки = Новый ДиалогВыбораФайла(РежимДиалогаВыбораФайла.ВыборКаталога);
			Если ДиалогВыбораПапки.Выбрать() Тогда
				ДиалогДанные.Вставить("Файл", ДиалогВыбораПапки.Каталог + "\scheta_random.txt");
				Возврат ДиалогДанные;
			КонецЕсли;
		КонецЕсли;
	КонецЕсли;
	Возврат Неопределено;
КонецФункции

// Генерирует случайный расчетный счет из групп кодов: ААА ББ ВВВ Г ДДДД ЕЕЕЕЕЕЕ,
// где ААА задается из справочника КодыРасчетногоСчета,
// ББ из ТабличнойЧасти справочника КодыРасчетногоСчета,
// ВВВ из справочника КодыВалютДляРачетногоСчета,
// Г, ДДДД, ЕЕЕЕЕЕЕ - случайные числа согласно разрядам.
// 
//  Параметры:
//  КодыА - Массив из Число - массив из всех кодов первого порядка справочника КодыРасчетногоСчета.
//  КодыВ - Массив из Число -  массив из всех кодов валюты справочника КодыВалютДляРачетногоСчета.
// 
// Возвращаемое значение:
//  Число - случайный расчетный счет
&НаСервере
Функция СгенерироватьСлучайныйРасчетныйСчет(КодыА, КодыВ)
	//TODO: Проверки пустоты ссылки,  массивов
	КодА = РандомайзерВызовСервера.ПолучитьСлучайныйЭлементИзМассива(КодыА);

	ТекстЗапроса = "ВЫБРАТЬ Ссылка ИЗ Справочник.КодыРасчетногоСчета ГДЕ КодПервогоПорядка=&КодПП";
	Ссылка = Простыезапросы.ВыполнитьЗапросПоПолю(ТекстЗапроса, "Ссылка", "КодПП", КодА);

	ТекстЗапроса = "
				   |ВЫБРАТЬ КодВторогоПорядка
				   |ИЗ Справочник.КодыРасчетногоСчета.КодыВторогоПорядка
				   |ГДЕ Ссылка=&Ссылка";
	МассивКодов = ПростыеЗапросы.ВыполнитьЗапросПоПолю(ТекстЗапроса, "КодВторогоПорядка", "Ссылка", Ссылка);

	КодБ = РандомайзерВызовСервера.ПолучитьСлучайныйЭлементИзМассива(МассивКодов);
	КодВ = РандомайзерВызовСервера.ПолучитьСлучайныйЭлементИзМассива(КодыВ);
	КодГ = РандомайзерВызовСервера.ПолучитьСлучайноеЧисло(0, 9);
	КодД = РандомайзерВызовСервера.ПолучитьСлучайноеЧисло(0, 9999);
	КодЕ = РандомайзерВызовСервера.ПолучитьСлучайноеЧисло(0, 9999999);

	КодБ = Формат(КодБ, "ЧЦ=2; ЧВН=; ЧГ=0");
	КодД = 	Формат(КодД, "ЧЦ=4; ЧВН=; ЧГ=0");
	КодЕ = Формат(КодЕ, "ЧЦ=7; ЧВН=; ЧГ=0");
	Результат = СтрШаблон("%1%2%3%4%5%6", КодА, КодБ, КодВ, КодГ, КодД, КодЕ);
	Возврат Число(Результат);
КонецФункции

// Получает массив кодов первого порядка из справочника КодыРасчетногоСчета (КодыА)
// и  массив из всех кодов валюты справочника КодыВалютДляРачетногоСчета (КодыВ).
// 
// Возвращаемое значение:
//  Структура - хранит два массива.
&НаСервере
Функция УзнатьАиВ()
	КодыРС = Новый Структура;
	КодыРС.Вставить("КодыА");
	КодыРС.Вставить("КодыВ");
	ТекстЗапроса = "ВЫБРАТЬ Справочник.КодыРасчетногоСчета.КодПервогоПорядка";
	КодыРС.КодыА =  Простыезапросы.ВыполнитьЗапросПоПолю(ТекстЗапроса, "КодПервогоПорядка");
	ТекстЗапроса = "ВЫБРАТЬ Справочник.КодыВалютДляРасчетныхСчетов.КодВалюты";
	КодыРС.КодыВ = Простыезапросы.ВыполнитьЗапросПоПолю(ТекстЗапроса, "КодВалюты");
	Возврат КодыРС;
КонецФункции

&НаСервере
Функция ТекДата()
	Возврат ТекущаяДатаСеанса();
КонецФункции
#КонецОбласти