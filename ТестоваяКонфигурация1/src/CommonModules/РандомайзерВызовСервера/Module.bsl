#Область ПрограммныйИнтерфейс

// Получить случайное число.
// 
// Параметры:
//  От - Число - Левая граница включительно
//  До - Число - Правая граница включительно
// 
// Возвращаемое значение:
//  Число - случайное число
Функция ПолучитьСлучайноеЧисло(От, До) Экспорт
	ГСЧ = Новый ГенераторСлучайныхЧисел;
	Возврат ГСЧ.СлучайноеЧисло(От, До)
КонецФункции

// Генерирует случайный расчетный счет из групп: ААА ББ ВВВ Г ДДДД ЕЕЕЕЕЕЕ,  где ААА (406, 407, 408), 
//ББ (для ААА=406 от 01 до 06, для ААА=407 от 01 до 07, а для ААА=408 либо 02 либо 17)
// 
// Возвращаемое значение:
//  Число - случайный расчетный счет
Функция СгенерироватьСлучайныйРасчетныйСчет() Экспорт
	ААА = ПолучитьСлучайноеЧисло(406, 408);
	ББ = 0;
	Если ААА = 406 Тогда
		ББ = ПолучитьСлучайноеЧисло(1, 6);
	ИначеЕсли ААА = 407 Тогда
		ББ = ПолучитьСлучайноеЧисло(1, 7);
	ИначеЕсли ААА = 408 Тогда
		ББ=?(ПолучитьСлучайноеЧисло(0, 1) = 0, 2, 17);
	КонецЕсли;

	ВВВ = ?(ПолучитьСлучайноеЧисло(0, 1) = 0, 810, 156);
	Г = ПолучитьСлучайноеЧисло(0, 9);
	ДДДД = ПолучитьСлучайноеЧисло(0, 9999);
	ЕЕЕЕЕЕЕ = ПолучитьСлучайноеЧисло(0, 9999999);

	ББ = Формат(ББ, "ЧЦ=2; ЧВН=; ЧГ=0");
	ДДДД = 	Формат(ДДДД, "ЧЦ=4; ЧВН=; ЧГ=0");
	ЕЕЕЕЕЕЕ = Формат(ЕЕЕЕЕЕЕ, "ЧЦ=7; ЧВН=; ЧГ=0");
	Возврат Число("" + ААА + ББ + ВВВ + Г + ДДДД + ЕЕЕЕЕЕЕ);
КонецФункции

#КонецОбласти


#Область СлужебныеПроцедурыИФункции

// Добавляет ведущие нули к числу, длина которого меньше требуемой. Аналог команды Формат.
// 
// Параметры:
//  Икс - Число - число
//  ТребуемаяДлина  - Число -  длина числа с ведущими нулями
// 
// Возвращаемое значение:
//  Строка - вернет ведущие нули + исходное число
Функция ПолучитьСтрокуИзЧислаСВедущимиНулями(Знач Икс, Знач ТребуемаяДлина)
	РазрядыУИкс = НайтиКолвоРазрядов(Икс);
	Результат = Строка(Икс);
	Если РазрядыУИкс < ТребуемаяДлина Тогда
		НольСтр = "";
		Для Индекс = 1 По ТребуемаяДлина - РазрядыУИкс Цикл
			НольСтр = НольСтр + "0";
		КонецЦикла;
		Результат = НольСтр + Результат;
	КонецЕсли;
	Возврат Результат;
КонецФункции

// Функция для получения количества разрядов числа
//Параметры:
//  Икс - Число - число, у которого узнать количество разрядов
//Возвращаемое значение:
//  Число - Количество разрядов
Функция НайтиКолвоРазрядов(Знач Икс)
	Разряды = 1;
	Пока Икс > 9 Цикл
		Разряды = Разряды + 1;
		Икс = Цел(Икс / 10);
	КонецЦикла;
	Возврат Разряды;
КонецФункции

#КонецОбласти